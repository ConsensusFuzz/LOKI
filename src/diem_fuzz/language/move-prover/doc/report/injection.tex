\SubSection{Specification Injection}

Move specifications are reduced to basic assume/assert statements injected into
the Move code. As usual, an assume statement formulates a condition which can be
assumed to hold for verification at a given program point, whereas an assert
statement a condition which needs to be verified.


Specification instrumentation starts from a program for which references have
been removed, and mutation is transformed into a read-update-write cycle
(Sec.~\ref{sec:RefElim}).


\SubSubSection{Modular Verification}
\label{sec:ModularVerification}

Modular verification applies to all types of injections, and its principles are
therefore described first. When the Move prover is run, it takes as input a set
of Move modules which is closed under the transitive dependency relation (module
imports). However, only a subset of those modules are \emph{verification target}
(typically just one module). It is assumed that the tool environment ensures
that modules in the dependency relation which are not target of verification
have already successfully verified.

From the set of target modules, the set of \emph{target functions} is
derived. This set might be enriched by additional functions which need
verification because of global invariants, as discussed in
Sec.~\ref{sec:GlobalInvariants}. The resulting set of target functions will then
be verified one-by-one, assuming that any called functions have successfully
verified. If a called function is among the target functions, it might in fact
not verify; however, in this case a verification error will be reported at the
called function, and the verification result at the caller side can be ignored.

\SubSubSection{Pre- and Postconditions}

The injection of basic function specifications is illustrated in
Fig.~\ref{fig:RequiresEnsuresAbortsIf}.  An extension of the Move source
language is used to specify abort behavior. With~%
|fun f() { .. } onabort { conditions }| a Move function is defined where
|conditions| are assume or assert statements that are evaluated at every program
point the function aborts (either implicitly or with an |abort| statement). This
construct simplifies the presentation and corresponds to a per-function abort
block on bytecode level which is target of branching.

\begin{figure}[t!]
  \caption{Requires, Ensures, and AbortsIf Injection}
  \label{fig:RequiresEnsuresAbortsIf}
  \centering
\begin{MoveBox}
  fun f(x: u64, y: u64): u64 { x + y }
  spec f {
    requires x < y;
    aborts_if x + y > MAX_U64;
    ensures result == x + y;
  }
  fun g(x: u64): u64 { f(x, x + 1) }
  spec g {
    ensures result > x;
  }
  @\transform@
  fun f(x: u64, y: u64): u64 {
    spec assume x < y;
    let result = x + y;
    spec assert result == x + y;     // ensures of of
    spec assert                      // negated abort_if of f
      !(x + y > MAX_U64); @\label{line:aborts_holds_not}@
    result
  } onabort {
    spec assert                      // abort_if of f
      x + y > MAX_U64; @\label{line:aborts_holds}@
  }
  fun g(x: u64): u64 {
    spec assert x < x + 1;           // requires of f
@$\textrm{\it if inlined}$\label{line:inline}@
    let result = inline f(x, x + 1);
@$\textrm{\it elif opaque}$\label{line:opaque}@
    if (x + x + 1 > MAX_U64) abort;  // aborts_if of f
    spec assume result == x + x + 1; // ensures of f
@$\textrm{\it endif}$@
    spec assert result > x;          // ensures of g
    result
  }
\end{MoveBox}
\end{figure}

An aborts condition is translated into two different asserts: one where the
function aborts and the condition must hold (line~\ref{line:aborts_holds}), and
one where it returns and the condition must \emph{not} hold
(line~\ref{line:aborts_holds_not}). If there are multiple |aborts_if|, they are
or-ed. If there is no aborts condition, no asserts are generated. This means
that once a user specifies aborts conditions, they must completely cover the
abort behavior of the code. (The prover also provides an option to relax this
behavior, where aborts conditions can be partial and are only enforced on
function return.)

For a function call site we distinguish two variants: the call is \emph{inlined}
(line~\ref{line:inline}) or it is \emph{opaque} (line~\ref{line:opaque}). In
both cases, it is assumed that the called function is verified (see Modular
Verification, Sec.~\ref{sec:ModularVerification}). For inlined calls, the
function definition, with all injected assumptions and assertions turned into
assumptions (as those are considered proven) is substituted. For opaque
functions the specification conditions are inserted as
assumptions. Methodlogically, opaque functions need precise specificatons
relative to a particular objective, where as in the case of inlined functions
the code is still the source of truth and specifications can be partial or
omitted. However, inlining does not scale arbitrarily, and can be only used for
small function systems.

Notice we have not discussed the way how to deal with relating pre and post
states yet, which requires taking snapshots of state (e.g.~%
|ensures x == old(x) + 1|); the example in
Fig.~\ref{fig:RequiresEnsuresAbortsIf} does not need it. Snapshoting of state
will be discussed for global update invariants in Sec.~\ref{sec:GlobalInvariants}.

\SubSubSection{Modifies}

\begin{figure}[t!]
  \caption{Modifies Injection}
  \label{fig:Modifies}
  \centering
\begin{MoveBox}
  fun f(addr: address) { move_to<T>(addr, T{}) }
  spec f {
    pragma opaque;
    ensures exists<T>(addr);
    modifies global<T>(addr);
  }
  fun g() { f(0x1) }
  spec g {
    modifies global<T>(0x1); modifies global<T>(0x2);
  }
  @\transform@
  fun f(addr: address) {
    let can_modify_T = {addr};         // modifies of f
    spec assert addr in can_modify;    // permission check move_to @%
                                            \label{line:modifies_permission}@
    move_to<T>(addr, T{});
  }
  fun g() {
    let can_modify_T = {0x1, 0x2};     // modifies of g
    spec assert {0x1} <= can_modify_T; // permission check call f @%
                                            \label{line:modifies_call_permission}@
    spec havoc global<T>(0x1);         // havoc memory modified by f @%
                                            \label{line:modifies_havoc}@
    spec assume exists<T>(0x1);        // ensures of f
  }
\end{MoveBox}
\end{figure}


The |modifies| condition specifies that a function only changes specific memory.
It comes in the form |modifies global<T>(addr)|, and its onjection is
illustrated in Fig.~\ref{fig:Modifies}.

A type check is used to ensure that if a function specificies a~%
modifies all called functions which are \emph{opaque} declare modifies for the
same type |T|. This is important so we can relate the callees memory
modifications to that what is allowed at caller side.

At verification time, when an operation is performed which modifies memory, an
assertion is emitted that modification is allowed
(e.g. line~\ref{line:modifies_permission}). The permitted addresses derived from
the modifies clause are stored in a set |can_modify_T| generated by the
transformation. Instructions which modify memory are either primitives (like
|move_to| in the example) or function calls. If the function call is inlined,
modifies injection proceeds (conceptually) with the inlined body. For opaque
function calls, the static analysis has ensured that the target has a modifies
clause.  This clause is used to derive the modified memory, which must be a
subset of the modified memory of the caller
(line~\ref{line:modifies_call_permission}).

For opaque calls, we also need to \emph{havoc} the memory they modify
(line~\ref{line:modifies_havoc}), by which is meant assigning an unconstraint
value to it. If present, |ensures| from the called function, injected as
subsequent assumptions, are further constraining the modified memory.


\SubSubSection{Data Invariants}

\begin{figure}[t!]
  \caption{Data Invariant Injection}
  \label{fig:DataInvariants}
  \centering
\begin{MoveBox}
  struct S { a: u64, b: u64 }
  spec S { invariant a < b }
  fun f(s: S): S { let r = &mut s; r.a = r.a + 1; r.b = r.b + 1; s }
  @\transform@
  fun f(s: S): S {
    spec assume s.a < s.b;      // assume invariant for parameter
    let r = Mvp::local(s, F_s); // begin mutation of s
    r = Mvp::set(r, Mvp::get(r)[a = Mvp::get(r).a + 1]);
    r = Mvp::set(r, Mvp::get(r)[b = Mvp::get(r).b + 1]);
    spec assert                 // end mutation: invariant enforced
      Mvp::get(r).a < Mvp::get(r).b;
    s = Mvp::get(r);            // write back to s
    s
  }
\end{MoveBox}
\end{figure}

A data invariant specifies a constraint over a struct value. The value is
guaranteed to satisfy this constraint at any time. Thus, when a value is
constructed, the data invariant needs to be verified, and when it is consumed,
it can be assumed to hold.

In Move's reference semantics, construction of struct values is often done via a
sequence of mutations via mutable references. It is desirable that \emph{during}
such mutations, assertion of the data invariant is suspended. This allows to
state invariants which reference multiple fields, where the fields are updated
step-by-step.  Move's borrow semantics and concept of mutations provides a
natural way how to defer invariant evaluation: at the point a mutable reference
is released, mutation ends, and the data invariant can be enforced.  In other
specification formalisms, we would need a special language construct for
invariant suspension. Fig.~\ref{fig:DataInvariants} gives an example, and shows
how data invariants are reduced to assert/assume statements.

\Paragraph{Implementation}

The implementation hooks into the reference elimination
(Sec.~\ref{sec:RefElim}). As part of this the lifetime of references is
computed. Whenever a reference is released and the mutated value is written
back, we also enforce the data invariant. In addition, the data invariant is
enforced when a struct value is directly constructed.



\SubSubSection{Global Invariants}
\label{sec:GlobalInvariants}

Global invariants appear on Move module level and constraint the content of the
memory. While the basic injection of global invariants is relative simple, they
cause significant complexity with features like modular verification, suspension,
and generics. We first discuss the basic model, then extend it stepwise.

\Paragraph{Basic Global Invariants}

\begin{figure}[t!]
  \caption{Basic Global Injection}
  \label{fig:GlobalInvariants}
  \centering
\begin{MoveBox}
  fun f(a: address) {
    let r = borrow_global_mut<S>(a);
    r.value = r.value + 1
  }
  invariant [I1] forall a: address: global<S>(a).value > 0;
  invariant [I2] update
      forall a: address: global<S>(a).value > old(global<S>(a).value);
  @\transform@
  fun f(a: address) {
    spec assume I1;
    Mvp::snapshot_state(I2_BEFORE);
    r = <increment mutation>;
    spec assert I1;
    spec assert I2[old = I2_BEFORE];
  }
\end{MoveBox}
\end{figure}

Fig.~\ref{fig:GlobalInvariants} contains an example for the supported invariant
types and their injection into code. The first invariant, |I1|, is a regular
state invariant. It is assumed on function entry, and asserted after the state
update. The second, |I2|, is a state update invariant, which relates pre and
post states. For this a state snapshot is stored under some label |I2_BEFORE|,
which is then used in an assertion.

Notice that invariant injection can lead to inconsistencies. Consider the following
code fragment:

\begin{Move}
  invariant [I] forall a: address: global<S>(a).value > 0;
  @\transform@
  spec assume global<S>(0).value == 0;  // context, e.g. from a requires
  spec assume I;                        // injected
\end{Move}

\noindent We currently do not check whether an invariant is satisfiable
before we assume it, but rather rely on a generic consistency checker for
specifications.

The global invariant injection is optimized by knowledge of the prover, obtained
by static analysis, about (transitively) accessed memory. For opaque functions
(including also builtin functions) this information is obtained via the modifies
clause. For other function it is determined from the code. The full story is
more evolved because of the presence of both generic invariants and functions,
and is discussed below.

\Paragraph{Genericity}

\begin{figure}[t!]
  \caption{Genericity}
  \label{fig:Genericity}
  \centering
\begin{MoveBox}
  invariant [I1] global<S<u64>>(0).value > 1;
  invariant [I2] <T> global<S<T>>(0).value > 0;
  fun f(a: address) { borrow_global_mut<S<u8>>.value = 2 }
  fun g<R>(a: address) { borrow_global_mut<S<R>>.value = 3 }
  @\transform@
  fun f(a: address) {
    spec assume I2[T = u8];
    <<mutate>>
    spec assert I2[T = u8];
  }
  fun g<R>(a: address) {
    spec assume I1;
    spec assume I2[T = R];
    <<mutate>>
    spec assert I1;
    spec assert I2[T = R];
  }
\end{MoveBox}
\end{figure}

In the case of generic invariants and functions, we must use \emph{type
  unification} to determine which invariants are injected. Consider the example
in Fig.~\ref{fig:Genericity}. Invariant |I1| holds for a specific type
instantiation |S<u64>|, whereas |I2| is generic over all type instantiations for
|S<T>|.

The non-generic function |f| which works on the instantiation |S<u8>| will have
to inject the \emph{specialized} instance |I2[T = u8]|. The invariant |I1|,
however, does not apply for this function, because there is no overlap with
|S<u64>|.  In contrast, in the generic function |g| we have to inject both
invariants. Because this function works on arbitrary instances, it is also
relevant for the specific case of |S<u64>|.

In the general case, we are looking at a unification problem of the following
kind. Given the accessed memory of a function |f<R>| and an invariant |I<T>|, we
compute the pairwise unification of memory types. Those types are parameterized
over |R| resp. |T|, and successfull unification will result in a substitution
for both. On successfull unification, we include the invariant with |T| specialized
according to the substitution.

Notice that there are implications related to monomorphization coming from the
injection of global invariants; those are discussed in Sec.~\ref{sec:Mono}.


\Paragraph{Modular Verification}

\Paragraph{Disabling Invariants}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:
